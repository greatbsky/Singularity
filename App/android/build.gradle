// Top-level build file where you can add configuration options common to all sub-projects/modules.
// Define versions in a single place
ext {
//    buildToolsVersion = '26.0.2'    //rootProject.ext.buildToolsVersion
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }

    // 导入全局变量
    apply from: "global.gradle"

    dependencies {
        classpath a.plugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath greenrobot.plugin
    }
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven { url "https://jitpack.io" }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

subprojects {
    def isAppModule = it.name.equals('app')
    def enableDataBinding = isAppModule || it.name.equals('databinding')

    apply plugin: isAppModule? 'com.android.application': 'com.android.library'

    if(!isAppModule) {
        android {
            compileSdkVersion a.compileSdkVersion as Integer
            buildToolsVersion a.buildToolsVersion as String

            defaultConfig {
                minSdkVersion a.minSdkVersion as Integer
                targetSdkVersion a.targetSdkVersion as Integer
                versionCode app.versionCode as Integer
                versionName app.versionName as String

                testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
            }

            if(enableDataBinding){
                dataBinding {
                    enabled true
                }
            }
        }

        dependencies {
            compile fileTree(dir: 'libs', include: ['*.jar'])
            compile 'com.android.support:appcompat-v7:23.4.0'
            testCompile 'junit:junit:4.12'
            androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
                exclude group: 'com.android.support', module: 'support-annotations'
            })
        }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}